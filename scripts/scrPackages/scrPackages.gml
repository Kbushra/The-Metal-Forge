///Welcome to the new SIGNAL system

///@func send_package(receiver, package_name, value)
///@param receiver {asset}
///@param package_name {string}
///@param value {any}
///@desc To send a package to another object
function send_package(_id, _packageName, _value) 
{
	if !instance_exists(_id) { return; }
	
	var _variableName = _packageName + "_package";
	variable_instance_set(_id, _variableName, _value);
}

///@func send_persistent_package(receiver, package_name, type, value)
///@param receiver {asset}
///@param signal_name {string}
///@param type {carrier enum}
///@param value {any}
///@desc To carry a package that persists between rooms, directed at an instance or room
function send_persistent_package(_id, _packageName, _targetType, _value)
{
	var carrier = noone;
	
	with (gameCarrier)
	{
		if value_type == carrierValue.package && target_type == _targetType &&
			target == _id && name == _packageName { value = _value; carrier = id; }
	}
	
	if carrier == noone
	{
		var carrier = instance_create_depth(0, 0, 0, gameCarrier,
			{ name: _packageName, value: _value, value_type: carrierValue.package,
				target: _id, target_type: _targetType });
	}
	
	//If id exists the origin is set
	try { carrier.origin = id; }
	catch(error) { }
	
	return carrier;
}

///@func send_place_package(signal_name, value)
///@param package_name {string}
///@param value {any}
///@desc To send a package to the entire room
function send_place_package(_packageName, _value)
{
	return send_persistent_package(room, _packageName, carrierTarget.place, _value);
}

///@func package_contents(package_name)
///@param package_name {string}
///@desc Get the contents of a package. Returns NONE if package doesn't exist.
function package_contents(_packageName)
{
	var _variableName = _packageName + "_package";
	if !variable_instance_exists(id, _variableName) { return NONE; }
	return variable_instance_get(id, _variableName);
}

///@func place_package_contents(package_name)
///@param package_name {string}
///@desc Get the contents of a place package. Returns NONE if package doesn't exist.
function place_package_contents(_packageName)
{
	with (gameCarrier)
	{
		if value_type == carrierValue.package && target_type == carrierTarget.place &&
			room == target && name == _packageName { return value; }
	}
	
	return NONE;
}

///@func remove_package(package_name)
///@param package_name {string}
///@desc Removes a package
function remove_package(_packageName)
{
    var _variableName = _packageName + "_package";
    variable_instance_set(id, _variableName, NONE);
}

///@func remove_place_package(_packageName)
///@param package_name {string}
///@desc Removes a place package
function remove_place_package(_packageName)
{
	with (gameCarrier)
	{
		if value_type == carrierValue.package && target_type == carrierTarget.place &&
			room == target && name == _packageName { instance_destroy(); }
	}
}